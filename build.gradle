plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'jacoco'
    id 'java-test-fixtures'
}

pitest {
    junit5PluginVersion = '0.15'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
}

group = 'com.optivem'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def integrationTest = sourceSets.create('integrationTest')
configurations[integrationTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)
configurations[integrationTest.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    testClassesDirs = integrationTest.output.classesDirs
    classpath = configurations[integrationTest.runtimeClasspathConfigurationName] + integrationTest.output
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql:42.3.6'

    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:2.7.3'
    implementation 'de.huxhorn.sulky:de.huxhorn.sulky.ulid:8.3.0'
    implementation 'net.sizovs:pipelinr:0.7'
    implementation 'de.mkammerer.snowflake-id:snowflake-id:0.0.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.0'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.11'
    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    testImplementation 'com.athaydes:spock-reports:2.3.0-groovy-3.0', { transitive = false }

    integrationTestImplementation project

    // TODO: Check if any of these are redundant and then remove
    testFixturesImplementation 'net.sizovs:pipelinr:0.7'
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-test'
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.0'
    testFixturesImplementation 'org.codehaus.groovy:groovy-all:3.0.11'
    testFixturesImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    testFixturesImplementation 'com.athaydes:spock-reports:2.3.0-groovy-3.0', { transitive = false }
    testFixturesImplementation 'org.junit.jupiter:junit-jupiter-api'
    testFixturesImplementation 'org.assertj:assertj-core'
    // testFixturesCompileOnly 'org.junit.jupiter:junit-jupiter-api'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('integrationTest') {
    useJUnitPlatform()
}
