plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'jacoco'
}

pitest {
    junit5PluginVersion = '0.15'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

}

allprojects {
    group = 'com.optivem.kata.banking'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    configurations {
        compileOnly.extendsFrom annotationProcessor
        testCompileOnly.extendsFrom annotationProcessor
        testAnnotationProcessor.extendsFrom annotationProcessor
    }

    repositories {
        mavenCentral()
    }

    // TODO: VC: Move these files into respective modules
    dependencies {
        compileOnly 'org.springframework.boot:spring-boot-starter-webflux:3.0.1' // Needed for HTTP client

        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'org.postgresql:postgresql:42.3.6'

        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.0'
        implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:2.7.3'
        implementation 'de.huxhorn.sulky:de.huxhorn.sulky.ulid:8.3.0'
        implementation 'net.sizovs:pipelinr:0.7'
        implementation 'de.mkammerer.snowflake-id:snowflake-id:0.0.1'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.0'
        testImplementation 'org.codehaus.groovy:groovy-all:3.0.11'
        testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
        testImplementation 'com.athaydes:spock-reports:2.3.0-groovy-3.0', { transitive = false }
        testImplementation 'au.com.dius:pact-jvm-consumer-junit5:4.0.10' // NOTE: For contract testing
        testImplementation 'au.com.dius:pact-jvm-consumer-java8:4.0.10' // NOTE: For contract testing
        testImplementation 'au.com.dius:pact-jvm-provider-junit5:4.0.10' // NOTE: For contract testing
    }

    test {
        useJUnitPlatform()
    }

}

task coreTest {
    dependsOn 'core:test'
}

task fakeAdapterTest {
    dependsOn 'adapter-generation-fake:test'
    dependsOn 'adapter-messaging-fake:test'
    dependsOn 'adapter-persistence-fake:test'
    dependsOn 'adapter-time-fake:test'
}

task realAdapterTest {
    dependsOn 'adapter-generation-random:test'
    dependsOn 'adapter-messaging-inmemory:test'
    dependsOn 'adapter-persistence-jpa:test'
    dependsOn 'adapter-persistence-mongo:test'
    dependsOn 'adapter-restapi-spring:test'
    dependsOn 'adapter-time-system:test'
}

task adapterTest {
    dependsOn fakeAdapterTest
    dependsOn realAdapterTest
}

task systemTest {
    dependsOn 'startup:test'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}